finder
  form(class='pure-form')
    input#in(onkeyup='{ change }', autofocus)
  .emojis
    emojis(data='{ emojis }', tagsearch='{ tagsearch }')

  script(type='text/coffeescript').
    @emojis = []
    fuse = original = null

    aja()
      .method 'get'
      .url '/data/emojis.json'
      .on 'success', (emojis)=>
        original = for emoji in emojis
          name: emoji.name
          unicode: emoji.keywords[emoji.keywords.length-1]
          keywords: emoji.keywords.slice 0, -1
        @update
          emojis: original
        fuse = new Fuse original,
          threshold: 0.1
          distance: 800
          keys: ['name', 'keywords']
        hash = location.hash.replace /#/, ''
        document.getElementById('in').value = hash
        @change
          target:
            value: hash
      .go()

    @tagsearch = (event)=>
      document.getElementById('in').value = event.target.innerText
      @change
        target:
          value: event.target.innerText

    @change = (event)=>
      searchWord = event.target.value
      @update
        emojis: if searchWord.length is 0 then original else fuse.search searchWord
      location.hash = searchWord
